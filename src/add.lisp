(in-package #:clifford)

(defun %binary-addition (info)
  `(defmethod add ((x ,(name info)) (y ,(name info)))
     (,(constructor info)
       ,@(iter (for a in-accessors-of info)
               (collecting `(+ (,a x) (,a y)))))))

(defun %scalar-addition (info)
  `((defmethod add ((x ,(name info)) (y ,(scalar-type info)))
      (,(constructor info)
        ,(first (keywords info)) (+ (,(first (accessors info)) x) y)
        ,@(iter (for a in-non-scalar-accessors-of info)
                (collecting `(,a x)))))
    (defmethod add ((x ,(scalar-type info)) (y ,(name info)))
      (,(constructor info)
        ,(first (keywords info)) (+ x (,(first (accessors info)) y))
        ,@(iter (for a in-non-scalar-accessors-of info)
                (collecting `(,a y)))))))

(defun create-addition-functions (info)
  `(,(%binary-addition info)
    ,@(%scalar-addition info)))
